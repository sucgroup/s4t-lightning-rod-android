apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "ru.esmukov.kpfu.lightningrodandroid"
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:support-vector-drawable:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.apache.commons:commons-compress:1.13'

    testCompile 'junit:junit:4.12'
    compile 'org.jetbrains:annotations-java5:15.0'
}


apply plugin: 'com.moowork.node'

node {
    // Enabled the automatic download.
    download = true
    version = '6.10.0'
    npmVersion = '3.10.10'

    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")

    nodeModulesDir = file("${project.projectDir}/../s4t-lightning-rod")
}

task cleanJs(type: NpmTask) {
    args = ['run', 'clean']
}
// todo clean assets
clean.dependsOn cleanJs



task buildJs(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'build']
}

task copyDistTarJs(type: Copy, dependsOn: buildJs)
copyDistTarJs {
    from '../s4t-lightning-rod/target/'
    into 'src/main/assets/js_target/'
    duplicatesStrategy = 'include'

    // workaround `.gz` part being stripped
    // http://stackoverflow.com/questions/22796903/asset-is-renamed-from-tar-gz-to-tar-after-packing-apk
    // http://stackoverflow.com/questions/4666098/why-does-android-aapt-remove-gz-file-extension-of-assets
    rename { String fileName ->
        fileName.replace('dist.tar.gz', 'dist.tar.gz_')
    }
}

preBuild.dependsOn copyDistTarJs
